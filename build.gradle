plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
}

version = "${minecraft_version}-${mod_version}"
group = 'platinpython.vfxgenerator'
archivesBaseName = 'VFX Generator'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

jarJar.enable()

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: 'parchment', version: '2023.07.16-1.20.1'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run/client')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            args '--username', 'Player###'

            mods {
                vfxgenerator {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            mods {
                vfxgenerator {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run/data')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            args '--mod', 'vfxgenerator', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                vfxgenerator {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.20.1-47.1.3'

    minecraftLibrary 'org.l33tlabs.twl:pngdecoder:1.0'
    jarJar('org.l33tlabs.twl:pngdecoder:[1.0,)') {
        transitive(false)
    }

    runtimeOnly fg.deobf("curse.maven:atlasviewer-633577:4762356")
}

tasks.jarJar.configure {
    archiveClassifier = ''
}

jar {
    archiveClassifier = 'slim'

    manifest {
        attributes([
                "Specification-Title"     : "vfxgenerator",
                "Specification-Vendor"    : "PlatinPython",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "PlatinPython",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}